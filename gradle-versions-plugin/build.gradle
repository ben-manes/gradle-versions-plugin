plugins {
  id 'org.jetbrains.kotlin.jvm'
  id 'org.jetbrains.dokka'
  id 'org.jlleitschuh.gradle.ktlint'
  id 'com.gradle.plugin-publish'
  id 'maven-publish'
  id 'java-gradle-plugin'
  id 'java-library'
  id 'groovy'
}

group = GROUP
version = VERSION_NAME

// Write the plugin's classpath to a file to share with the tests
tasks.register('createClasspathManifest') {
  def outputDir = file("$buildDir/$name")

  inputs.files sourceSets.main.runtimeClasspath
  outputs.dir outputDir

  doLast {
    outputDir.mkdirs()
    file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
  }
}

dependencies {
  compileOnly gradleApi()

  implementation localGroovy()
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.0.0'
  implementation 'org.jetbrains.kotlin:kotlin-reflect:2.0.0'
  implementation 'com.squareup.okhttp3:okhttp:4.12.0'
  implementation 'com.squareup.moshi:moshi-kotlin:1.12.0'

  testRuntimeOnly files(createClasspathManifest)

  testImplementation localGroovy()
  testImplementation gradleTestKit()
  testImplementation 'org.spockframework:spock-junit4:2.1-M2-groovy-3.0', { exclude module: 'groovy-all' } // Use localGroovy()
}

tasks.register('docsJar', Jar) {
  dependsOn dokkaHtml
  group = 'Publications'
  description = 'Create jar of documentation.'
  archiveClassifier = 'javadoc'
  from dokkaJavadoc.outputDirectory
}

tasks.register('sourcesJar', Jar) {
  dependsOn classes
  group = 'Publications'
  description = 'Create jar of sources.'
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

publishing {
  publications {
    pluginMaven(MavenPublication) {
      artifact docsJar
      artifact sourcesJar

      pom {
        resolveStrategy = Closure.DELEGATE_FIRST
        name = POM_NAME
        description = POM_DESCRIPTION
        url = POM_URL
        inceptionYear = POM_INCEPTION_YEAR

        licenses {
          license {
            name = POM_LICENSE_NAME
            url = POM_LICENSE_URL
            distribution = POM_LICENSE_DIST
          }
        }

        developers {
          developer {
            id = 'ben-manes'
            name = 'Ben Manes'
            email = 'ben.manes@gmail.com'
          }
          developer {
            id = 'zenedith'
            name = 'Zenedith'
            email = 'zenedith@wp.pl'
          }
          developer {
            id = 'jochenberger'
            name = 'Jochen Berger'
          }
          developer {
            id = 'jaredsburrows'
            name = 'Jared Burrows'
            email = 'jaredsburrows@gmail.com'
          }
        }

        scm {
          url = POM_SCM_URL
          connection = POM_SCM_CONNECTION
          developerConnection = POM_SCM_DEV_CONNECTION
        }

        issueManagement {
          system = POM_ISSUE_SYSTEM
          url = POM_ISSUE_URL
        }
      }
    }
  }
}

gradlePlugin {
  website = POM_URL
  vcsUrl = POM_SCM_URL
  plugins {
    versionsPlugin {
      id = 'com.github.ben-manes.versions'
      implementationClass = 'com.github.benmanes.gradle.versions.VersionsPlugin'
      displayName = POM_NAME
      description = POM_DESCRIPTION
      tags.set(['dependencies', 'versions', 'updates'])
    }
  }
}
