plugins {
  id 'com.gradle.plugin-publish' version '0.15.0'
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'codenarc'
  id 'groovy'
}

repositories {
  mavenCentral()
}

group = 'com.github.ben-manes'
version = '0.42.0'

sourceCompatibility = JavaVersion.VERSION_1_8

buildScan {
  termsOfServiceUrl = 'https://gradle.com/terms-of-service'
  termsOfServiceAgree = 'yes'
}

codenarcMain {
  configFile = file('config/codenarc/rules.groovy')
}

codenarcTest {
  configFile = file('config/codenarc/testRules.groovy')
}

tasks.withType(CodeNarc) { codeNarcTask ->
  codeNarcTask.ignoreFailures = true
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
  def outputDir = file("$buildDir/$name")

  inputs.files sourceSets.main.runtimeClasspath
  outputs.dir outputDir

  doLast {
    outputDir.mkdirs()
    file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
  }
}

// Add the classpath file to the test runtime classpath
dependencies {
  implementation 'com.thoughtworks.xstream:xstream:1.4.19'

  testRuntimeOnly files(createClasspathManifest)

  testImplementation gradleTestKit()
  testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
    exclude(module: 'groovy-all')
  }
}

jar {
  manifest {
    attributes(
      'Implementation-Title': 'Gradle Versions plugin',
      'Implementation-Version': archiveVersion,
      'Built-By': System.getProperty('user.name'),
      'Built-JDK': System.getProperty('java.version'),
      'Built-Gradle': gradle.gradleVersion
    )
  }
}

tasks.withType(Test) {
  maxParallelForks Runtime.getRuntime().availableProcessors()

  testLogging {
    events "passed", "failed", "skipped"
    showStandardStreams true
    exceptionFormat "full"
  }
}

task docsJar(type: Jar, dependsOn: groovydoc) {
  group = 'Publications'
  description = 'Create jar of documentation.'
  archiveClassifier = 'docs'
  from groovydoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
  group = 'Publications'
  description = 'Create jar of sources.'
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

task testsJar(type: Jar) {
  group = 'Publications'
  description = 'Create jar of tests.'
  archiveClassifier = 'tests'
  from sourceSets.test.output
}

task reportsZip(type: Zip, dependsOn: check) {
  group = 'Publications'
  description = 'Create a zip of all reports.'
  archiveClassifier = 'reports'
  from reporting.baseDir
}

// Local published to ~/.m2 - mavenLocal()
publishing {
  repositories {
    mavenLocal()
  }

  publications {
    pluginMaven(MavenPublication) {
      artifact docsJar
      artifact sourcesJar
      artifact testsJar
      artifact reportsZip

      pom {
        resolveStrategy = Closure.DELEGATE_FIRST
        name = 'Gradle Versions plugin'
        description = 'Gradle plugin that provides tasks for discovering dependency updates.'
        url = 'https://github.com/ben-manes/gradle-versions-plugin'
        inceptionYear = '2012'

        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'ben-manes'
            name = 'Ben Manes'
            email = 'ben.manes@gmail.com'
          }
          developer {
            id = 'zenedith'
            name = 'Zenedith'
            email = 'zenedith@wp.pl'
          }
          developer {
            id = 'jochenberger'
            name = 'Jochen Berger'
          }
        }

        issueManagement {
          system = 'github'
          url = 'https://github.com/ben-manes/gradle-versions-plugin/issues'
        }

        scm {
          url = 'https://github.com/ben-manes/gradle-versions-plugin'
          connection = 'scm:https://ben-manes@github.com/ben-manes/gradle-versions-plugin.git'
          developerConnection = 'scm:git://github.com/ben-manes/gradle-versions-plugin.git'
        }
      }
    }
  }
}
publish.dependsOn jar, docsJar, sourcesJar, testsJar, reportsZip
publish.dependsOn 'generatePomFileForPluginMavenPublication'

gradlePlugin {
  plugins {
    versionsPlugin {
      id = 'com.github.ben-manes.versions'
      displayName = 'Gradle Versions Plugin'
      implementationClass = 'com.github.benmanes.gradle.versions.VersionsPlugin'
      description = 'Gradle plugin that provides tasks for discovering dependency updates.'
    }
  }
}

pluginBundle {
  website = 'https://github.com/ben-manes/gradle-versions-plugin'
  vcsUrl = 'https://github.com/ben-manes/gradle-versions-plugin'
  tags = ['dependencies', 'versions', 'updates']
}
