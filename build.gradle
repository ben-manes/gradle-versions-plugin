import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

plugins {
  id 'org.jetbrains.kotlin.jvm' version '2.0.0' apply false
  id 'org.jetbrains.dokka' version '1.8.10' apply false
  id 'org.jlleitschuh.gradle.ktlint' version '12.1.1' apply false
  id 'com.gradle.plugin-publish' version '0.21.0' apply false
  id 'java-gradle-plugin'
  id 'java-library'
  id 'groovy'
}

allprojects {
  configurations.configureEach {
    resolutionStrategy {
      preferProjectModules()

      enableDependencyVerification()

      eachDependency { details ->
        if (details.requested.group == "org.jetbrains.kotlin") {
          details.useVersion("2.0.0")
        }
      }
    }
  }
}

subprojects {
  repositories {
    mavenCentral()
  }

  tasks.withType(Jar).configureEach {
    manifest {
      attributes(
        'Implementation-Title': POM_NAME,
        'Implementation-Version': VERSION_NAME,
        'Built-By': System.getProperty('user.name'),
        'Built-JDK': System.getProperty('java.version'),
        'Built-Gradle': gradle.gradleVersion)
    }
  }

  tasks.withType(KotlinJvmCompile).configureEach {
    compilerOptions {
      jvmTarget.set(JvmTarget.JVM_1_8)
    }
  }

  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    configure(options) {
      compilerArgs << '-Xlint:all'
      compilerArgs << '-Xlint:-options'
      encoding = 'utf-8'
      fork = true
    }
  }

  tasks.withType(GroovyCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    configure(options) {
      compilerArgs << '-Xlint:all'
      compilerArgs << '-Xlint:-options'
      encoding = 'utf-8'
      fork = true
    }
  }

  tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
      exceptionFormat 'full'
      showCauses true
      showExceptions true
      showStackTraces true
      showStandardStreams true
      events 'passed', 'failed', 'skipped'
    }

    def maxWorkerCount = gradle.startParameter.maxWorkerCount
    maxParallelForks = (maxWorkerCount < 2) ? 1 : maxWorkerCount / 2
  }
}
