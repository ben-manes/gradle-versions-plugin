apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'jacoco'
apply plugin: 'java'
defaultTasks 'dependencyUpdates'

buildscript {
  repositories {
    // Use 'gradle install' to install latest
    mavenLocal()
    jcenter()
  }

  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:+'
  }
}

repositories {
  jcenter()
}

configurations {
  bom
  upToDate
  exceedLatest
  platform
  upgradesFound
  upgradesFound2
  unresolvable
  unresolvable2
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
  checkForGradleUpdate = true

  // Example 1: reject all non stable versions
  rejectVersionIf {
    isNonStable(candidate.version)
  }

  // Example 2: disallow release candidates as upgradable versions from stable versions
  rejectVersionIf {
    isNonStable(candidate.version) && !isNonStable(currentVersion)
  }

  // Example 3: using the full syntax
  resolutionStrategy {
    componentSelection {
      all {
        if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
          reject('Release candidate')
        }
      }
    }
  }
}

dependencies {
  bom 'org.springframework.boot:spring-boot-dependencies:1.5.8.RELEASE',
      'com.google.code.gson:gson',
      'dom4j:dom4j'
  upToDate 'backport-util-concurrent:backport-util-concurrent:3.1',
           'backport-util-concurrent:backport-util-concurrent-java12:3.1'
  exceedLatest 'com.google.guava:guava:99.0-SNAPSHOT',
               'com.google.guava:guava-tests:99.0-SNAPSHOT'
  upgradesFound 'com.google.guava:guava:15.0',
                'com.google.inject:guice:2.0',
                'com.google.inject.extensions:guice-multibindings:2.0'
  upgradesFound2 'com.google.guava:guava:16.0-rc1'
  unresolvable 'com.github.ben-manes:unresolvable:1.0',
               'com.github.ben-manes:unresolvable2:1.0'
  unresolvable2 'com.github.ben-manes:unresolvable:1.0',
                'com.github.ben-manes:unresolvable2:1.0'

  platform 'com.linecorp.armeria:armeria',
           'io.zipkin.brave:brave'
  // Common usage would be to separate this into a project that uses the `java-platform` plugin to
  // share constraints among several projects.
  constraints {
    platform 'com.linecorp.armeria:armeria:0.90.0',
             'io.zipkin.brave:brave:5.7.0'

  }
}
